plugins {
  id 'idea'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'scala'
  id 'io.miret.etienne.sass' version '1.1.2'
}

idea {
  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}

// There is no Java in the project :)
project.gradle.startParameter.excludedTaskNames.add('compileJava')

tasks.withType(ScalaCompile) {
  scalaCompileOptions.with {
//  targetCompatibility = '1.8'
    additionalParameters = [
      '-new-syntax',
      '-feature',
      '-language:strictEquality',
      //'-source:future',
      //'-deprecation',
      //'-unchecked',
      //'-Xsemanticdb',
      //'-Ysafe-init',
      //'-explain',
    ]
  }
}

description = '19 Kislev Archive'

final String opentorahVersion  = '0.4.1-SNAPSHOT'

dependencies {
  implementation 'org.scala-lang:scala3-library_3:3.1.1-RC1'
  implementation "org.opentorah:opentorah-collector:$opentorahVersion"
  implementation "org.opentorah:opentorah-base:$opentorahVersion" // for the Cutter
}

compileSass {
  group = 'publishing'

  version = '1.34.0'

  sourceDir = project.file ("$rootDir/sass")
  outputDir = project.file ("$rootDir/css")
}

task buildSite(type: JavaExec) {
  group = 'publishing'
  dependsOn(compileSass)

  mainClass = 'org.opentorah.collector.Collector'
  args = ['buildSite', rootDir]
  classpath = sourceSets.main.runtimeClasspath
}

task buildAndPrettyPrintSite(type: JavaExec) {
  group = 'publishing'
  dependsOn(compileSass)

  mainClass = 'org.opentorah.collector.Collector'
  args = ['buildAndPrettyPrintSite', rootDir]
  classpath = sourceSets.main.runtimeClasspath
}

final String gcloudServiceAccountKey =
  findProperty('gcloudServiceAccountKey') ?:
  System.getenv('gcloudServiceAccountKey') ?:
  "nokey"

task uploadSite(type: JavaExec) {
  group = 'publishing'
  dependsOn(buildSite)

  mainClass = 'org.opentorah.collector.Collector'
  args = ['uploadSite', rootDir, gcloudServiceAccountKey]
  classpath = sourceSets.main.runtimeClasspath
}

task uploadSiteDryRun(type: JavaExec) {
  group = 'publishing'
  dependsOn(buildSite)

  mainClass = 'org.opentorah.collector.Collector'
  args = ['uploadSiteDryRun', rootDir, gcloudServiceAccountKey]
  classpath = sourceSets.main.runtimeClasspath
}

task serveSite(type: JavaExec) {
  group = 'publishing'

  mainClass = 'org.opentorah.collector.Collector'
  args = ['serveSite', "file://$rootDir/"]
  classpath = sourceSets.main.runtimeClasspath
}

task serveRemoteSite(type: JavaExec) {
  group = 'publishing'

  mainClass = 'org.opentorah.collector.Collector'
  args = ['serveRemoteSite']
  classpath = sourceSets.main.runtimeClasspath
}
