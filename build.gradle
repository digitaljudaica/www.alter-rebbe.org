plugins {
  id 'idea'
  id 'com.github.ben-manes.versions' version '0.44.0'
  id 'scala'
  id 'io.miret.etienne.sass' version '1.4.2'
}

idea {
  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}

// There is no Java in the project :)
project.gradle.startParameter.excludedTaskNames.add('compileJava')

tasks.withType(ScalaCompile).configureEach {
  scalaCompileOptions.with {
//  targetCompatibility = '1.8'
    additionalParameters = [
      '-new-syntax',
      '-feature',
      '-language:strictEquality',
      //'-source:future',
      //'-deprecation',
      //'-unchecked',
      //'-Xsemanticdb',
      //'-Ysafe-init',
      //'-explain',
    ]
  }
}

description = '19 Kislev Archive'

final String opentorahVersion  = '0.10.8'

dependencies {
  implementation 'org.scala-lang:scala3-library_3:3.2.2'
  implementation "org.opentorah:opentorah-collector:$opentorahVersion"
  implementation "org.opentorah:opentorah-util:$opentorahVersion" // for the Cutter
}

compileSass {
  group = 'publishing'

  version = '1.34.0'

  sourceDir = project.file ("$rootDir/sass")
  outputDir = project.file ("$rootDir/css")
}

tasks.register('buildSite', JavaExec) {
  group = 'publishing'
  dependsOn(compileSass)

  mainClass.set('org.opentorah.collector.Collector')
  args = ['buildSite', rootDir]
  classpath = sourceSets.main.runtimeClasspath
}

tasks.register('buildAndPrettyPrintSite', JavaExec) {
  group = 'publishing'
  dependsOn(compileSass)

  mainClass.set('org.opentorah.collector.Collector')
  args = ['buildAndPrettyPrintSite', rootDir]
  classpath = sourceSets.main.runtimeClasspath
}

final String gcloudServiceAccountKey =
  findProperty('cloud-run-deploy_alter-rebbe-2') ?:
  System.getenv('gcloudServiceAccountKey') ?:
  "nokey"

tasks.register('uploadSite', JavaExec) {
  group = 'publishing'
  dependsOn(buildSite)

  mainClass.set('org.opentorah.collector.Collector')
  args = ['uploadSite', rootDir, gcloudServiceAccountKey]
  classpath = sourceSets.main.runtimeClasspath
}

tasks.register('uploadSiteDryRun', JavaExec) {
  group = 'publishing'
  dependsOn(buildSite)

  mainClass.set('org.opentorah.collector.Collector')
  args = ['uploadSiteDryRun', rootDir, gcloudServiceAccountKey]
  classpath = sourceSets.main.runtimeClasspath
}

tasks.register('serveSite', JavaExec) {
  group = 'publishing'

  mainClass.set('org.opentorah.collector.Collector')
  args = ['serveSite', "file://$rootDir/"]
  classpath = sourceSets.main.runtimeClasspath
}

tasks.register('serveRemoteSite', JavaExec) {
  group = 'publishing'

  mainClass.set('org.opentorah.collector.Collector')
  args = ['serveRemoteSite']
  classpath = sourceSets.main.runtimeClasspath
}
